// (c) https://github.com/MontiCore/monticore

/* ============================================================ */
/* ========================= Plugins ========================== */
/* ============================================================ */

plugins {
  id "de.monticore.generator" version "$mc_version"
  id "com.github.johnrengelman.shadow" version "7.0.0"
  id "io.github.themrmilchmann.ecj" version "0.2.0" apply(false)
}

/* ============================================================ */
/* ========================= Project ========================== */
/* ============================================================ */

description = 'SysML_v2_official'

/* ============================================================ */
/* ========================= Versions ========================= */
/* ============================================================ */


def commons_cli_version = "1.4"
def commons_lang_version = "3.11"

/* ============================================================ */
/* ======================= Configuration ====================== */
/* ============================================================ */


test {
  useJUnitPlatform()
}

/* ============================================================ */
/* ======================= Dependencies ======================= */
/* ============================================================ */

dependencies {
  // language dependencies
  grammar "de.monticore.lang:ocl:$mc_version"
  grammar project(":cc")

  implementation "de.monticore:stream-symbols:$mc_version"

  implementation "org.apache.commons:commons-lang3:$commons_lang_version"
  implementation "commons-cli:commons-cli:$commons_cli_version"
}

/* ============================================================ */
/* ========================== Tasks =========================== */
/* ============================================================ */


// Fixes issue with java plugin in projects without resources
compileTestJava.doFirst { mkdir sourceSets.main.output.resourcesDir }

// static delegate of "Log" prohibits parallel execution of tests that check the log...
tasks.withType(Test) {
  maxParallelForks = 1
}

tasks.withType(JavaCompile) {
  options.fork = true
  options.encoding = "UTF-8"
  options.deprecation false
  options.warnings = false
  options.headerOutputDirectory.convention(null)
}

// all in one cli-jar
shadowJar {
  manifest {
    attributes "Main-Class": " de.monticore.lang.sysmlv2.SysMLv2Tool"
  }
  archiveClassifier = 'cli'
  minimize()
}

java {
//  withJavadocJar()
  withSourcesJar()
}


/* ============================================================ */
/* ======================= Publishing ========================= */
/* ============================================================ */

publishing {
  // configure what artifacts to publish
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories.maven {
    name = "Nexus"
    credentials.username nexusUser
    credentials.password nexusPassword
    def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
    def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
  }
}

defaultTasks('build')
