/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang;

component grammar SysMLImportsAndPackages extends SysMLBasis {

  SysMLImportStatement extends MCImportStatement implements SysMLElement =
      Modifier "import" MCQualifiedName (("::" | ".") Star:["*"])?
        ("as" Name)? ";" ;

  SysMLAlias implements SysMLElement =
    Modifier "alias" SysMLIdentifier? Name? "for" MCQualifiedName
    ("{" SysMLElement* "}" | ";") ;

  symbol scope SysMLPackage implements SysMLElement =
    ["standard"]? ["library"]? "package" SysMLIdentifier? Name? ("{"
      SysMLElement*
    "}" | ";") ;

  /**
   * Metadata is additional data that can be used to annotate
   * the elements of a model.
   */
  SysMLMetaDataDefinition implements SysMLElement =
    "metadata" "def" SysMLIdentifier? Name Specialization* ("{" SysMLElement* "}" | ";") ;

  SysMLMetaDataUsage implements SysMLElement =
    ("metadata" | "@") SysMLIdentifier? Name ("{" SysMLElement* "}" | ";") ;

}
