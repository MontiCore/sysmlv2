/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang;

grammar SysMLv2 extends SysMLExpressions,
                        SysMLImportsAndPackages,
                        SysMLActions,
                        SysMLParts,
                        SysMLStates,
                        SysMLConstraints,
                        SysMLConnections,
                        SysMLOccurrences,
                        SysMLViews,
                        SysMLCases,
                        ComponentConnector,
                        de.monticore.types.MCSimpleGenericTypes,
                        Automaton,
                        de.monticore.expressions.AssignmentExpressions,
                        de.monticore.expressions.StreamExpressions
{

  start SysMLModel ;

  SysMLModel = SysMLElement* ;

  // Bug in MontiCore fÃ¼hrt dazu, dass es nur hier funktioniert
  @Override
  token Char = "doNotParseThisEverAgain";

  @Override
  token Name =
    ( ( 'a'..'z' | 'A'..'Z' | '_' | '$' )
      ( 'a'..'z' | 'A'..'Z' | '_' | '0'..'9' | '$' )* )
    | ( '\'' Anything* '\'' ) :
    {
      if(getText().length() > 0 && getText().charAt(0) == '\'')
        setText(getText().substring(1, getText().length() - 1));
    } ;

  // Do not include "//*" since its considered a "MultilineNote"
  @Override
  token SL_COMMENT =
    "//" (~'*') (~('\n' | '\r'))* : ->channel(HIDDEN);

  FractalLiteral implements NumericLiteral =
    { noSpace(2) }? "." post:Digits;

  /**
   * A user-defined keyword for semantic metadata may also be used to declare a
   * definition or usage without using any language-defined keyword.
   */
  symbol scope UserDefinedDefinition implements SysMLType =
    Modifier UserDefinedKeyword+ "def" Name SysMLCardinality? Specialization*
    ("{"
      SysMLElement*
    "}" | ";") ;

  symbol scope UserDefinedUsage implements SysMLElement =
    Modifier UserDefinedKeyword+ Name SysMLCardinality? Specialization*
    ("{"
      SysMLElement*
    "}" | ";") ;

}
