/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang;

/**
 * Adaptation to PartDefinitions
 */
grammar SysML4VerificationBlockDiagrams extends SysMLBlockDiagrams {

  // Causality indication: "instant" = weak causal, "delayed" = strong causal. Defaults to "instant".
  SysMLCausality = "timing" (["instant"] | ["delayed"]) ";";

  // Trustlevel indication of a part which is given relative to its environment / enclosing part def
  TrustLevel = "trustLevel" (["+"] | ["-"]) NatLiteral reason:StringLiteral? ";";

  // TrustLevelRelation between part properties of a part def
  TrustLevelRelation = "trustLevelRelation" left:Name (["<"] | [">"] | ["=="]) right:Name ";";

  // Refinement relations. Adaptation of "SysMLSpecialization"
  SysMLRefinement = "refines" roughDef:(MCObjectType || ",")+;

  /**
   * Adds "SysMLRefinement?" to the header and "SysMLCausality | RequirementUsage" to body.
   * CoCo enforces limit of one causality.
   */
  @Override
  PartDef =
    SysMLModifier? ("part" "def" | "block") Name SysMLSpecialization? SysMLRefinement?
    ("{"
      (BDUsageMember | SysMLElement | SysMLUsage | SysMLImportStatement | SysMLCausality | TrustLevel | TrustLevelRelation)*
    "}" | ";") ;

}
