plugins {
  id "application"
  id 'de.monticore.language-server' version "$mclsg_version"
  id "com.github.johnrengelman.shadow" version "4.0.4"
}

def logback_version = '1.1.2'

mainClassName = "de.monticore.lang.sysml4verification._lsp.LanguageServerCLI"

tasks.build.dependsOn shadowJar

configurations {
  grammar // Dependencies in this scope are consumed by mclsg to generate lsp
}

dependencies {
  // Language and parent language
  grammar(project(":language")) {
    exclude group: 'de.monticore', module: 'monticore-grammar'
    capabilities {
      requireCapability("de.monticore.lang:language-grammars")
    }
  }
  grammar(project(":language4verification")) {
    exclude group: 'de.monticore', module: 'monticore-grammar'
    capabilities {
      requireCapability("de.monticore.lang:language4verification-grammars")
    }
  }
  // MPf: Kopiert von MontiArc wegen Dopplung der Grammatik in "normaler" JAR und "-grammars" JAR
  grammar ("de.monticore:monticore-grammar:$mc_version") {
    capabilities {
      requireCapability("de.monticore:monticore-grammar-grammars")
    }
  }

  implementation(project(":language"))
  implementation(project(":language4verification"))
  // Dependencies of sysml4verification, all marked as runtime in pom => we need to specify them here
  //implementation "de.monticore.lang:ocl:7.0.0"
  implementation "de.monticore:monticore-grammar:$mc_version"
  implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
  implementation "de.monticore:monticore-runtime:$mc_version"
  implementation "org.antlr:antlr4-runtime:4.7.1"
  implementation "org.jgrapht:jgrapht-core:1.3.1"
  implementation "de.monticore:mc-assemblies:0.0.6"
  implementation "de.monticore.lang:ocl:7.0.0" // MPf: Ganz komische Fehler wegen "*OCL*", diese Zeile hatte Alex drin

  // MCLSG
  implementation "de.monticore.language-server:monticore-language-server-runtime:$mclsg_version"

  modules {
    // somewhere in monticore some bootstrap (bs) versions were erroneously passed on
    module("de.monticore.bs:monticore-grammar") {
      replacedBy("de.monticore:monticore-grammar")
    }
  }
}

test {
  useJUnitPlatform()
}

import de.mclsg.task.*
task generateLanguageServer(type: MCLSGTask) {
  mclsg {
    languageName = "de.monticore.lang.SysML4Verification"
    fileExtension = "sysml"
    outputDir = "$buildDir/generated-sources/mclsg/sourcecode"
    handCodedDir = "$projectDir/src/main/java"
    // Symbols that are added to the Language via Java, not the grammar
    /*additionalSymbolNames = [
      "de.monticore.lang.sysmlparametrics._symboltable.ConstraintUsage", // Fix - manuell hinzu generiert import in CompletionProvider
      "de.monticore.lang.sysmlrequirementdiagrams._symboltable.RequirementUsage",
      "de.monticore.lang.sysmlstatemachinediagrams._symboltable.StateUsage"
  ]*/
  }
}

// configure non-standard source sets
sourceSets {
  main.java.srcDirs += [
      "$buildDir/generated-sources/mclsg/sourcecode"
  ]
}

compileJava {
  dependsOn generateLanguageServer
}

shadowJar { // all in one jar
  manifest {
    attributes "Main-Class": mainClassName
  }
  archiveClassifier = "jar-with-dependencies"
}

runShadow {

}
