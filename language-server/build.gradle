/* (c) https://github.com/MontiCore/monticore */
plugins {
  id "application"
  id 'de.monticore.language-server' version "$mclsg_version"
  id 'de.monticore.generator' version "$mc_version" // prepare the grammar configuration
}

mainClassName = "de.monticore.lang.sysmlv2._lsp.LanguageServerCLI"

dependencies {
  // Language and parent language
  grammar(project(":language"))

  implementation(project(":cc"))

  // Dependencies marked as "runtime" in the language's build script. Need to re-specify them here!
  implementation "de.monticore:monticore-grammar:$mc_version"
  implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
  implementation "de.monticore:monticore-runtime:$mc_version"
  implementation "org.antlr:antlr4-runtime:4.7.1"
  implementation "org.jgrapht:jgrapht-core:1.3.1"
  implementation "de.monticore:mc-assemblies:0.0.6"
  implementation "de.monticore.lang:ocl:7.0.0" // MPf: Ganz komische Fehler wegen "*OCL*", diese Zeile hatte Alex drin

  // MCLSG
  implementation "de.monticore.language-server:monticore-language-server-runtime:$mclsg_version"

  modules {
    // somewhere in monticore some bootstrap (bs) versions were erroneously passed on
    module("de.monticore.bs:monticore-grammar") {
      replacedBy("de.monticore:monticore-grammar")
    }
  }
}

test {
  useJUnitPlatform()
}


import de.mclsg.TaskTypes
import de.mclsg.task.*
import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

task autoconfigure(type: AutoconfigureTask) {
  mclsg {
    languageName = "de.monticore.lang.SysMLv2"
    fileExtension = "sysml"
  }
  including(TaskTypes.VSCODE_PLUGIN)
}
tasks.build.dependsOn autoconfigure

// Edit package.json of the generated project
task editPackageJson() {
  doLast {
    def file = new File("${autoconfigure.mclsg.getFullVscodePluginDir()}/package.json")
    def json = new JsonBuilder(new JsonSlurper().parse(file))
    json.content.version = "$version"
    json.content.publisher = "MontiBelle"
    json.content.name = "sysmlv2" // No spaces, no capitals
    json.content.icon = "icons/logo.png"
    json.content.homepage = "https://github.com/MontiCore/sysmlv2"
    json.content.repository = [
        "type": "git",
        "url": "https://github.com/MontiCore/sysmlv2"
    ]

    json.content.contributes.languages[0].icon = [
        "light": "icons/logo.png",
        "dark": "icons/logo.png"
    ]
    json.content.contributes.languages[0].configuration = "./language-configuration.json"

    file.write(json.toPrettyString())
  }
}

task copyReadme(type: Copy) {
  from "$rootDir"
  include "README.md"
  into(autoconfigure.mclsg.getFullVscodePluginDir())
}

task copyFiles(type: Copy) {
  from "$projectDir"
  include "icons/**"
  include "language-configuration.json"
  into autoconfigure.mclsg.getFullVscodePluginDir()
}

def vscodePlugin = new File("${autoconfigure.mclsg.getFullVscodePluginDir()}/sysmlv2-${version}.vsix")

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "language-server"
      from components.java

      artifact(vscodePlugin) {
        classifier = "vscode"
        extension = "vsix"
        builtBy tasks.named("generateSysMLv2VscodePlugin")
      }
    }
  }
  repositories.maven {
    name = "Nexus"
    credentials.username nexusUser
    credentials.password nexusPassword
    def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
    def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
  }
  repositories.maven {
    name = "Github"
    url = "https://maven.pkg.github.com/MontiCore/sysmlv2"
    credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
    }
  }
}

tasks.generateSysMLv2VscodePlugin.finalizedBy editPackageJson
tasks.generateSysMLv2VscodePlugin.finalizedBy copyReadme
tasks.generateSysMLv2VscodePlugin.finalizedBy copyFiles
