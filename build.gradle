/* (c) https://github.com/MontiCore/monticore */
subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  // Needs to be after java plugin
  sourceCompatibility = "11"

  project.buildDir = "target"

  repositories {
    // "nexus" also caches maven central
    maven {
      name "nexus"
      url = publicNexus
    }
  }

  // Java JDK Warning
  if (JavaVersion.current() > sourceCompatibility) {
    logger.warn("\u001B[33m Warning: You are currently using JDK Version " + JavaVersion.current() + ". " + "Higher JDK versions may cause compatibility problems. JDK 11 is recommended. \u001B[0m")
  }

  // Use the same testing framework for all modules
  dependencies {
    implementation "org.slf4j:slf4j-simple:2.0.0-beta1"
    // Test annotations, api
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    // Parametrized tests
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    // Runtime
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    // AssertJ fluent assertions "assertThat(subject).has(...).is(...).throws(...)
    testImplementation "org.assertj:assertj-core:$assertj_version"
  }
}

evaluationDependsOnChildren()
project(":language-server").tasks.generateSysMLv2LanguageServer.dependsOn project(":language").tasks.grammarsJar
