/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml.basics;


component grammar SysMLImportsAndPackages extends de.monticore.lang.sysml.basics.SysMLPackageBasis,
  de.monticore.lang.sysml.basics.SysMLBasics,
  de.monticore.lang.sysml.basics.SysMLVisibility,
  de.monticore.lang.sysml.basics.SysMLShared {

  /* IMPORTS */
  //TODO: Override MC Imports?
  AliasPackagedDefinitionMember implements SysMLType, PackagedDefinitionMember =
      ("import" | ["alias"]) MCQualifiedName ("as"  Name)? ";";

  /* PACKAGES */
  PackageUnit implements Unit =  Package;
  AnonymousPackage implements Unit = PackageBody;
  Package implements SysMLType, PackagedDefinitionMember =  PackageDeclaration PackageBody;
  astrule Package =
      method public String getName(){
         return this.getPackageDeclaration().getName();
      }
    ;
  PackageDeclaration = "package" Name;
  scope PackageBody = "{" (PackageMember | SysMLImportStatement)* "}";


  /* PACKAGE MEMBERSHIPS */
  PackageMember = PackageMemberPrefix ( PackagedDefinitionMember | PackagedUsageMember);
  PackageMemberPrefix = visibility:PackageElementVisibilityIndicator?;
  // Remark: PackagedDefinitionMember is an interface
  // Remark: PackagedUsageMember is an interface
}
