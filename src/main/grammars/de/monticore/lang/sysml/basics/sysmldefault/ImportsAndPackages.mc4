/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml.basics.sysmldefault;


component grammar ImportsAndPackages extends de.monticore.lang.sysml.basics.interfaces.CommentsBasis,
  de.monticore.lang.sysml.basics.interfaces.ImportBasis,
  de.monticore.lang.sysml.basics.interfaces.NamesBasis,
  de.monticore.lang.sysml.basics.interfaces.PackageBasis,
  de.monticore.lang.sysml.basics.interfaces.VisibilityBasis,
  de.monticore.lang.sysml.basics.interfaces.SharedBasis {

  /* IMPORTS */

  AliasPackagedDefinitionMember implements PackagedDefinitionMember =
      ("import" | "alias") QualifiedName ("as" SysMLName)? ";";
  //Following has the name "Import" in Xtext
  ImportUnitStd implements ImportUnit = PrefixAnnotation* (visibility:PackageElementVisibilityIndicator)?
    "import" QualifiedName (star:["::*"]? | star:[".*"]?) {noSpace()}? ("as" SysMLName)? ";";

  /* PACKAGES */
  PackageUnit implements Unit = UnitPrefix Package;
  AnonymousPackage implements Unit = PackageBody;
  Package implements PackagedDefinitionMember =  PackageDeclaration PackageBody;
  PackageDeclaration = "package" name:SysMLName;
  PackageBody = "{" (PackageMember | ImportUnit)* "}";

  /* PACKAGE MEMBERSHIPS */
  PackageMember = PackageMemberPrefix ( PackagedDefinitionMember | PackagedUsageMember);
  PackageMemberPrefix = PrefixAnnotation* visibility:PackageElementVisibilityIndicator?;
  // Remark: PackagedDefinitionMember is an interface
  // Remark: PackagedUsageMember is an interface
}
