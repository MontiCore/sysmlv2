/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml.basics;


component grammar SysMLUsages extends de.monticore.lang.sysml.basics.SysMLCommonBasis {

  /* USAGES */
  UsageStd implements Usage =
     UsageDeclaration UsageCompletion;
  astrule UsageStd =
    method public String getName(){
      return this.getUsageDeclaration().getName();
    }
  ;

  UsageDeclaration = (SysMLNameAndTypePart SubsettingPart)
    | (TypePart  SubsettingPart)
    | (RedefinesKeyword Redefinition TypePart? SubsettingPart); //SubsettingPart itself is optional
  astrule UsageDeclaration =
    method public String getName(){
      if( this.isPresentSysMLNameAndTypePart()){
        return this.getSysMLNameAndTypePart().getName();
      }
      return "NotNamed1232454123534j4jn43";
    }
  ;


  UsageCompletion = ValuePart? DefinitionBody;

  ValuePart = "=" FeatureValue;
  FeatureValue = Expression;
  TypePartStd implements TypePart = (TypedByKeyword ((FeatureTyping || ",")+ | "any") MultiplicityPart? )
    | MultiplicityPart;
  TypedByKeyword = ":" | "typed" "by";
  //FeatureTypingStd implements FeatureTyping = QualifiedName; TODO
  //FeatureTypingStd implements Type, FeatureTyping = MCType;
  FeatureTypingStd implements FeatureTyping = name:QualifiedName;
  MultiplicityPart = MultiplicityMember (["ordered"]? ["nonunique"]? | ["nonunique"]? ["ordered"]?);
    //Sequence of keywords does not matter here.

  Multiplicity = "[" ((FromToDigitsDotDot)? UnlimitedNaturalLiteralMember) "]";
  SubsettingPart = (Subsets ("," Subset)* | Redefines  ("," Redefinition)*)*;

  Subsets = SubsetsKeyword Subset;
  SubsetsKeyword = ":>" | "subsets";
  Subset = QualifiedName;

  Redefines = RedefinesKeyword Redefinition+;
  RedefinesKeyword = ":>>" | "redefines";
  Redefinition = QualifiedName;

  //Usage Memberships
  MultiplicityMember = Multiplicity;

  //Remark: literal type is enforced by CoCo (write CoCo) or by Exact Literal TODO
  UnlimitedNaturalLiteralMember = limitedLiteral:Literal | isUnlimited:["*"];

  /* PARAMETERS */

  ParameterStd implements Parameter = ParameterTypePart?;
  ParameterTypePart = (TypedByKeyword (FeatureTyping | "any" ) MultiplicityPart? )
    | (MultiplicityPart);

  ActionOrStateUsageDeclaration = (SysMLNameAndTypePart ActionParameterList? SubsettingPart ValuePart?)
       | (TypePart? ActionParameterList? ValuePart?)
       | (RedefinesKeyword Redefinition TypePart ActionParameterList? ValuePart?);
}
