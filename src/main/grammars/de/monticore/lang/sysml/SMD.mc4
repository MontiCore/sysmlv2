/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml;

component grammar SMD extends de.monticore.lang.sysml.advanced.SysMLSuccessionsAndItemFlows {

  /* STATE DEFINITIONS */

  StateDefinitionUnit implements Unit = UnitPrefix StateDefinition;
  StateDefinition implements SysMLType, PackagedDefinitionMember = StateDefDeclaration StateBody;
  astrule StateDefinition =
    method public String getName(){
      return this.getStateDefDeclaration().getName();
    };
  StateDefDeclaration = ["abstract"]? "state" "def" SysMLName ParameterList? SuperclassingList?;
  astrule StateDefDeclaration =
    method public String getName(){
      return this.getSysMLName().getName();
    };

  scope StateBody = ";" | "{" StateBodyPart "}";
  StateBodyPart = (EntryActionMember EntryTransitionMember*)? DoActionMember? ExitActionMember? StateBodyItem*;
  StateBodyItem = NestedDefinitionMember
    | StructureUsageMember
    | StateMember TargetTransitionSuccessionMember*
    | TransitionStepMember
    | ImportUnit;
  StateActionUsage = EmptyActionUsage ";" | PerformedActionUsage ActivityBody;

  /* STATE MEMBERSHIPS */

  EntryActionMember = DefinitionMemberPrefix kind:EntryActionKind StateActionUsage;
  EntryActionKind = "entry";
  DoActionMember = DefinitionMemberPrefix kind:DoActionKind StateActionUsage;
  DoActionKind = "do";
  ExitActionMember = DefinitionMemberPrefix kind:ExitActionKind StateActionUsage;
  ExitActionKind = "exit";

  StateMember = DefinitionMemberPrefix ["abstract"]? ("ref" "state" | isComposite:["state"]) StateUsage;
  EntryTransitionMember = DefinitionMemberPrefix (GuardedTargetSuccession | "then" TransitionSuccession ) ";";

  TargetTransitionSuccessionMember =  DefinitionMemberPrefix TargetTransitionStep ";";
  TransitionStepMember = DefinitionMemberPrefix TransitionStep ";";

 /* STATES */
  StateUnit implements Unit = UnitPrefix StateDeclaration StateBody;
  StateDeclaration = ["abstract"]? "state" SysMLNameAndTypePart ActionParameterList? SubsettingPart ValuePart?;
  StateUsage = ActionOrStateUsageDeclaration StateBody;
  ExhibitStateUsage implements SysMLType = ((((SysMLName) TypePart? "as")? Subset)
    | ( "state" SysMLName? TypePart? )) ActionParameterList? SubsettingPart ValuePart? StateBody;
  astrule ExhibitStateUsage =
    method public String getName(){
      if( this.isPresentSysMLName()){
        return this.getSysMLName().getName();
      }
      return "";
    }
  ;


  StateUsagePackagedUsageMember implements PackagedUsageMember =       ["abstract"]? "state" StateUsage;
  BehaviorUsageMemberStateUsage implements BehaviorUsageMember =
    DefinitionMemberPrefix ["abstract"]?  ("ref" "state" | isComposite:"state") StateUsage;
  BehaviorUsageMemberExhibitStateUsage implements BehaviorUsageMember =
    DefinitionMemberPrefix  "exhibit" ExhibitStateUsage;
}
