/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml.advanced;


component grammar SysMLItemFlows extends de.monticore.lang.sysml.common.SysMLCommon {
 /* ITEM FLOWS */
  NonPortStructureUsageMemberItemFlow implements SysMLType, NonPortStructureUsageMember =
    ["abstract"]? "stream" ItemFlow;
  NonPortStructureUsageMemberSuccessionItemFlow implements SysMLType, NonPortStructureUsageMember =
    ["abstract"]? "flow" SuccessionItemFlow;

  ItemFlow = ItemFlowDeclaration DefinitionBody;
  SuccessionItemFlow = ItemFlowDeclaration DefinitionBody;
  //In the official implementation, there is just  Name? TypePart?,
  //but that leads to only specifying a type results in a name
  ItemFlowDeclaration = ( (typeOfFlow: Name | ( Name TypePart))?
    (( "of" ItemFeatureMember) | (EmptyItemFeatureMember))
    "from" | EmptyItemFeatureMember )
    ItemFlowEndMember "to" ItemFlowEndMember;


  ItemFeatureTyping = FeatureTyping MultiplicityMember?;
  EmptyItemFeature;
  ItemFlowEnd = ItemFlowFeatureMember;
  ItemFlowFeature = Redefinition;

  /* ITEM FLOW MEMBERSHIPS */
  ItemFeatureMember = (memberName: Name TypedByKeyword)? ItemFeatureTyping;
  ItemFlowEndMember = ItemFlowEnd;
  ItemFlowFeatureMember = ItemFlowFeature;
  EmptyItemFeatureMember = EmptyItemFeature;

  /* ===================================================*/
  /* ============ ASTRULES getName() for symbols =======*/
  /* ===================================================*/
  astrule NonPortStructureUsageMemberItemFlow =
    method public String getName(){
      return this.getItemFlow().getItemFlowDeclaration().getName();
    }
  ;
  astrule NonPortStructureUsageMemberSuccessionItemFlow =
    method public String getName(){
      return this.getSuccessionItemFlow().getItemFlowDeclaration().getName();
    }
  ;
}