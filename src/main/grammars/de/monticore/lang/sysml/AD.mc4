/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml;

component grammar AD extends de.monticore.lang.sysml.advanced.SysMLAdvanced {

  /* ACTIVITIES */

  ActivityUnit implements Unit = UnitPrefix Activity;
  Activity implements PackagedDefinitionMember = ActivityDeclaration ActivityBody;
  symbol ActivityDeclaration = ["abstract"]? "activity" SysMLName ParameterList? SuperclassingList?;
  astrule ActivityDeclaration =

     method public String getName(){
       return this.getSysMLName().getName();
     };

  ParameterListStd implements ParameterList = "(" (ParameterMember || "," )* ")";
  ActivityBodyStd implements ActivityBody = ";" | ("{" ActivityBodyItem* "}");
  ActivityBodyItemStd implements ActivityBodyItem =
    NestedDefinitionMember
    | StructureUsageMember
    | InitialNodeMember TargetSuccessionMember*
    | EmptySuccessionMember? ActivityNodeMember TargetSuccessionMember*
    | GuardedSuccessionMember
    | ImportUnit;


  /* ACTIVITIY MEMBERSHIPS */

  symbol ParameterMemberStd implements ParameterMember = direction:FeatureDirection? memberName:SysMLName Parameter;
  astrule ParameterMemberStd =
      method public String getName(){
        return this.getMemberName().getName();
      }
    ;
  InitialNodeMember = DefinitionMemberPrefix "first" memberFeature:QualifiedName ";";
  ActivityNodeMember = DefinitionMemberPrefix ActivityNode;
  TargetSuccessionMember = DefinitionMemberPrefix
    (DefaultTargetSuccession | TargetSuccession | GuardedTargetSuccession) ";";
  GuardedSuccessionMember = GuardedSuccession ";";


  AssociationEndMemberActionUsage implements AssociationEndMember =
     ["abstract"]?"end" ("ref" "action" | isComposite:"action" ) ActionUsage;

  /* ACTIONS */
  ActionUnit implements Unit = UnitPrefix ActionDeclaration ActivityBody;
  ActionDeclaration = ["abstract"]? "action" SysMLNameAndTypePart ActionParameterList? SubsettingPart ValuePart?;
  ActionUsageStd implements ActionUsage = ActionOrStateUsageDeclaration ActivityBody;

  ActionUsagePackagedUsageMember implements PackagedUsageMember =      ["abstract"]? "action" ActionUsage;
  BehaviorUsageMemberActionUsage implements BehaviorUsageMember =
    DefinitionMemberPrefix ["abstract"]?  ("ref" "action" | isComposite:"action") ActionUsage;
  BehaviorUsageMemberPerformActionUsage implements BehaviorUsageMember =
    DefinitionMemberPrefix "perform" PerformActionUsage;

  ActionParameterListStd implements ActionParameterList = "(" ( ActionParameterMemberAndFlowMember || ",")* ")";
  ActionParameterMemberAndFlowMember = ActionParameterMember ActionParameterFlowMember?;
  ActionParameter = ParameterTypePart? ValuePart?;
  ActionParameterFlow =
    ("stream" | "flow") EmptyItemFeatureMember "from" ItemFlowEndMember;

  PerformActionUsage = PerformActionUsageDeclaration ActivityBody;
  PerformActionUsageDeclaration = ((( SysMLNameAndTypePart "as" )? Subset+)
    |( "action" (name:SysMLName)? TypePart?)) ActionParameterList? SubsettingPart ValuePart?;

  /* ACTIVITY NODES */
  ActivityNode = ActionNode | ControlNode;
  ActionNode = ActionNodeDeclaration ActivityBody;
  ActionNodeDeclaration = ActionUsageNodeDeclaration | PerformActionNodeDeclaration | AcceptActionNodeDeclaration |
  SendActionNodeDeclaration;
  ActionUsageNodeDeclaration = ["abstract"]? (("ref" "action") | isComposite:["action"]) ActionOrStateUsageDeclaration;
  PerformActionNodeDeclaration = isComposite:["perform"] (SysMLNameAndTypePart "as")? Subset ActionParameterList?
    SubsettingPart ValuePart?;
  AcceptActionNodeDeclaration = EmptyParameterMember "accept" SysMLNameAndTypePart? "(" ItemFeatureMember ")";
  SendActionNodeDeclaration = EmptyParameterMember EmptyItemFeatureMember
    "send" ( SysMLNameAndTypePart? "of")? ExpressionMember "to" ExpressionMember;
  ControlNode = MergeNode	| DecisionNode | JoinNode | ForkNode;

  MergeNode = "merge" SysMLNameAndTypePart? ";";
  DecisionNode = "decide" SysMLNameAndTypePart? ";";
  JoinNode = "join" SysMLNameAndTypePart? ";";
  ForkNode = "fork" SysMLNameAndTypePart? ";";
  //TODO improve to? => ControlNode = (["merge"] | ["decide"] | ["join"] | ["fork"]) SysMLNameAndTypePart? ";";

  /* ACTION MEMBERSHIPS */
  ActionParameterMember = (direction:FeatureDirection | direction:FeatureDirection? memberName:SysMLName)
    ActionParameter;
  ActionParameterFlowMember = ActionParameterFlow;
  EmptySuccessionMember = EmptySuccession;

  PerformedActionUsageStd implements PerformedActionUsage = PerformActionUsageDeclaration | AcceptActionNodeDeclaration | SendActionNodeDeclaration;
}
