/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml.common;

component grammar SysMLAssociations extends de.monticore.lang.sysml.common.SysMLCommonBasis {

  /* ASSOCIATION BLOCKS */
  AssociationBlockUnit implements Unit = UnitPrefix AssociationBlock;
  AssociationBlock implements SysMLType, PackagedDefinitionMember =
    AssociationBlockDeclaration AssociationBlockBody;
  AssociationBlockDeclaration = ["abstract"]? "assoc" "block" ClassifierDeclarationCompletion;

  scope AssociationBlockBody = (";") | ("{"
    (
        NestedDefinitionMember
        | AssociationUsageMember
        | ImportUnit
    )* "}"
  );

  /* ===================================================*/
  /* ============ ASTRULES getName() for symbols =======*/
  /* ===================================================*/
  astrule AssociationBlock =
    method public String getName(){
      return this.getAssociationBlockDeclaration().getName();
    }
  ;
  astrule AssociationBlockDeclaration =
    method public String getName(){
      return this.getClassifierDeclarationCompletion().getName();
    }
  ;

  /* ASSOCIATION BLOCK MEMBERSHIPS */
  AssociationUsageMemberStd implements AssociationUsageMember =
    NestedUsageMember | AssociationEndMember | EndPortMember | ConjugatedEndPortMember;
  //NestedUsageMember is an interface and is implemented in SysMLCommon
  //AssociationEndMember is an interface and is not implemented here
  EndPortMember = DefinitionMemberPrefix ["abstract"]? "end" "port" PortUsage;
  ConjugatedEndPortMember = DefinitionMemberPrefix ["abstract"]? "end" "port" ConjugatedPortUsage;
}
