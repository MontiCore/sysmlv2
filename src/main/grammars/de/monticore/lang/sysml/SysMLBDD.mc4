/* (c) https://github.com/MontiCore/monticore */
package de.monticore.lang.sysml;

component grammar SysMLBDD extends de.monticore.lang.sysml.common.SysMLCommon {

  /* BLOCKS */

  BlockUnit implements Unit = UnitPrefix Block; //Remark: in Xtext BlockDeclaration DefinitionBody

  Block implements SysMLType, PackagedDefinitionMember = BlockDeclaration DefinitionBody;

  BlockDeclaration = ["abstract"]? ("part" "def" | "block") ClassifierDeclarationCompletion;

  AssociationEndMemberReferenceProperty implements SysMLType, AssociationEndMember =
     DefinitionMemberPrefix ["abstract"]? "end" ["ref"]? ReferenceProperty;
  ReferencePropertyNonPortStructureUsageMember implements SysMLType, NonPortStructureUsageMember =
     ["abstract"]? "ref" direction:FeatureDirection? ReferenceProperty;
  ReferenceProperty = Usage;

  /* ===================================================*/
  /* ============ ASTRULES getName() for symbols =======*/
  /* ===================================================*/
  astrule Block =
    method public String getName(){
      return this.getBlockDeclaration().getName();
    }
  ;
  astrule BlockDeclaration =
    method public String getName(){
      return this.getClassifierDeclarationCompletion().getName();
    }
  ;
  astrule ReferenceProperty =
    method public String getName(){
      return this.getUsage().getName();
    }
  ;
  astrule AssociationEndMemberReferenceProperty =
    method public String getName(){
      return this.getReferenceProperty().getName();
    }
  ;
  astrule ReferencePropertyNonPortStructureUsageMember =
    method public String getName(){
      return this.getReferenceProperty().getName();
    }
  ;
}
